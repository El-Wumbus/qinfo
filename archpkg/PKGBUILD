# Maintainer: Aidan Neal <decator(dot)c(at)proton(dot)me>
pkgname=qinfo-git
pkgver=r71.ce31f95
pkgrel=1
pkgdesc="A system info program. Fetches system info and displays it."
arch=('x86_64')
url="https://github.com/El-Wumbus/qinfo"
license=('GPL3')
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
makedepends=('git')
source=('git+https://github.com/El-Wumbus/qinfo.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/${pkgname%-git}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/${pkgname%-git}"

  # Don't override default CFLAGS
  sed -i 's/CFLAGS=/CFLAGS+=/g' Makefile
}

build() {
  cd "$srcdir/${pkgname%-git}"
  make
}

package() {
  cd "$srcdir/${pkgname%-git}"
  make DESTDIR="$pkgdir" install
}
